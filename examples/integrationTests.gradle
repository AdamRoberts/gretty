/*
Include this script in subprojects:
apply from rootProject.file('integrationTests.gradle')
*/

apply plugin: 'groovy' // this is needed for spock

ext {
  groovy_version = '2.3.3'
  logback_version = '1.1.2'
  spock_version = '0.7-groovy-2.0'
  gebVersion = '0.9.2'
  seleniumVersion = '2.42.2'
}

dependencies {
  testCompile "org.codehaus.groovy:groovy-all:$groovy_version"
  testCompile "org.spockframework:spock-core:$spock_version"
  testCompile "org.gebish:geb-spock:$gebVersion"
  testCompile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
  testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
}

test {
  include '**/Test*.*'
  include '**/*Test.*'
  exclude '**/*IT.*'
}

task integrationTest(type: Test, dependsOn: 'test') {
  outputs.upToDateWhen { false }
  include '**/*IT.*'
  doFirst {
    systemProperty 'geb.build.reportsDir', reporting.file('geb')
  }
}

