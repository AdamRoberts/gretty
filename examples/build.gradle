apply plugin: 'base' // add "clean" task to the root project.

ext {
  groovy_version = '2.3.0'
  logback_version = '1.1.2'
  spock_version = '0.7-groovy-2.0'
  gebVersion = '0.9.2'
  seleniumVersion = '2.40.0'
}

subprojects {
  if(project.subprojects.size() != 0)
    return
    
  apply plugin: 'groovy'

  repositories {
    mavenLocal()
    jcenter()
  }

  afterEvaluate {
    if(extensions.findByName('gretty')) {

      dependencies {
        testCompile "org.codehaus.groovy:groovy-all:$groovy_version"
        testCompile "org.spockframework:spock-core:$spock_version"
        testCompile "org.gebish:geb-spock:$gebVersion"
        testCompile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
        testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
      }

      test {
        include '**/Test*.*'
        include '**/*Test.*'
        exclude '**/*IT.*'
      }

      task integrationTest(type: Test, dependsOn: 'test') {
        outputs.upToDateWhen { false }
        include '**/*IT.*'
        systemProperty 'geb.build.reportsDir', reporting.file('geb')
      }

      gretty {
        integrationTestTask = 'integrationTest'
      }
    }
  }
}

