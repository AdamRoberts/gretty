apply plugin: 'base' // add "clean" task to the root project.

ext {
  groovy_version = '2.1.9'
  generateSources = true
  generateJavadoc = true
}

subprojects {
  apply plugin: 'maven'

  repositories {
    mavenLocal()
    mavenCentral()
  }

  group = 'org.akhikhl.gretty'
  version = '0.0.3'

  afterEvaluate {  
    build {
      finalizedBy install
    }

    if(generateSources && tasks.findByName('classes')) {
      task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates sources jar') {
        classifier = 'sources'
        from sourceSets.main.allSource
      }
      artifacts {
        archives sourcesJar
      }
    }

    if(generateJavadoc && tasks.findByName('javadoc')) {
      task javadocJar(type: Jar, description: 'Creates javadoc jar') {
        dependsOn javadoc
        classifier = 'javadoc'
        from javadoc.destinationDir
        if(tasks.findByName('groovydoc')) {
          dependsOn groovydoc
          from groovydoc.destinationDir
        }
      }
      artifacts {
        archives javadocJar
      }
    }
  }
}
