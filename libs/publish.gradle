// include this file in subprojects:
// apply from: rootProject.file('libs/publish.gradle')

apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'bintray'

def thisProject = project    

if(thisProject.hasProperty('publishToSonatype')) {
  
  assert thisProject.hasProperty('signing.keyId')
  assert thisProject.hasProperty('signing.password')
  assert thisProject.hasProperty('signing.secretKeyRingFile')
  assert thisProject.hasProperty('sonatypeUsername')
  assert thisProject.hasProperty('sonatypePassword')
  
  signing {
    sign configurations.archives
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { deployment -> signing.signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
          authentication(userName: project.sonatypeUsername, password: project.sonatypePassword)
        }

        pom.project {
          name thisProject.name
          packaging 'jar'
          description thisProject.project_description
          url thisProject.project_website

          scm {
            url thisProject.project_scm
            connection thisProject.project_scm
            developerConnection thisProject.project_scm
          }

          licenses {
            license {
              name thisProject.license
              url thisProject.license_url
              distribution 'repo'
            }
          }

          developers {
            developer {
              id thisProject.developer_id
              name thisProject.developer_name
            }
          }
        }
      }
    }
  } // uploadArchives
} // publishToSonatype

// used by bintray
publishing {
  publications {
    mavenAll(MavenPublication) {
      if (plugins.hasPlugin('java')) {
        from components.java
      }
      artifact sourcesJar {
        classifier "sources"
      }
      artifact javadocJar {
        classifier "javadoc"
      }
      pom.withXml {
        new NodeBuilder(current: asNode()).with {
          name thisProject.name
          packaging 'jar'
          description thisProject.project_description
          url thisProject.project_website

          scm {
            url thisProject.project_scm
            connection thisProject.project_scm
            developerConnection thisProject.project_scm
          }

          licenses {
            license {
              name thisProject.license
              url thisProject.license_url
              distribution 'repo'
            }
          }

          developers {
            developer {
              id thisProject.developer_id
              name thisProject.developer_name
            }
          }
        }
      }      
    }
  }
} // publishing

bintray {
  user = project.hasProperty('bintrayUser') ? project.bintrayUser : ''
  key = project.hasProperty('bintrayKey') ? project.bintrayKey : ''
  publications = ['mavenAll'] // When uploading Maven-based publication files
  pkg {
    repo = 'maven'
    name = thisProject.rootProject.project_id
    desc = thisProject.rootProject.description
    licenses = ['MIT']
    labels = ['gretty', 'jetty', 'gradle', 'plugin']
  }
  dryRun = false
}

