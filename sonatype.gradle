// This script deals with signing archives and uploading to sonatype repository.
// It is applied to every subproject.
// It is project-specific.

apply plugin: 'signing'

afterEvaluate {
  if((plugins.findPlugin('java') || plugins.findPlugin('groovy')) && 
    project.hasProperty('signing.keyId') && 
    project.hasProperty('sonatypeUsername') && 
    project.hasProperty('sonatypePassword')) {
    
    signing {
      sign configurations.archives
    }

    uploadArchives {
      repositories {
        mavenDeployer {
          beforeDeployment { deployment -> signing.signPom(deployment) }

          repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: project.sonatypeUsername, password: project.sonatypePassword)
          }

          pom.project {
            name project.name
            packaging 'jar'
            description 'Gradle plugin for running web-applications under jetty 8.1.8 and servlet API 3.0.1.'
            url 'https://github.com/akhikhl/gretty'

            scm {
              url 'scm:git@github.com:akhikhl/gretty.git'
              connection 'scm:git@github.com:akhikhl/gretty.git'
              developerConnection 'scm:git@github.com:akhikhl/gretty.git'
            }

            licenses {
              license {
                name 'The MIT License'
                url 'http://opensource.org/licenses/mit-license.php/'
                distribution 'repo'
              }
            }

            developers {
              developer {
                id 'akhikhl'
                name 'Andrey Hihlovskiy'
              }
            }
          }
        }
      }
    } // uploadArchives
  }   
}
